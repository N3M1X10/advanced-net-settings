@echo off
chcp 65001 >nul

cd /d "%~dp0"

set adm_arg=%1
if "%adm_arg%" == "admin" (
    title admin
) else (
    echo [93m[powershell] Requesting admin rights...
    powershell -NoLogo -NoProfile -NonInteractive -ExecutionPolicy Bypass -Command "Start-Process 'cmd.exe' -ArgumentList '/k \"\"%~f0\" admin\"' -Verb RunAs"
    exit /b
)

:: ÐŸÑ€Ð¾Ð²ÐµÑ€ÐºÐ° Ð¿Ñ€Ð°Ð² Ð°Ð´Ð¼Ð¸Ð½Ð¸ÑÑ‚Ñ€Ð°Ñ‚Ð¾Ñ€Ð°
net session >nul 2>&1
if %errorLevel% neq 0 (
    echo ÐžÐ¨Ð˜Ð‘ÐšÐ: Ð—Ð°Ð¿ÑƒÑÑ‚Ð¸Ñ‚Ðµ Ñ„Ð°Ð¹Ð» Ð¾Ñ‚ Ð¸Ð¼ÐµÐ½Ð¸ ÐÐ´Ð¼Ð¸Ð½Ð¸ÑÑ‚Ñ€Ð°Ñ‚Ð¾Ñ€Ð°!
    pause
    exit /b
)

:menu
setlocal enabledelayedexpansion
cls
set "choice_keys="
:: ÐšÐ¾Ð½Ñ„Ð¸Ð³ÑƒÑ€Ð°Ñ†Ð¸Ñ Ð¼ÐµÐ½ÑŽ (ÐœÐ°ÑÑÐ¸Ð² Ð´Ð°Ð½Ð½Ñ‹Ñ…)
set "menu_items[1]=GAMING: Max Responsiveness"
set "menu_items[2]=DOWNLOAD: Max Throughput"
set "menu_items[3]=RESTORE: Factory Defaults"
set "menu_items[x]=Ð’Ñ‹Ñ…Ð¾Ð´"

echo [93m=========================================[0m
echo [96m       Ð’Ð«Ð‘ÐžÐ  Ð Ð•Ð–Ð˜ÐœÐ ÐÐÐ¡Ð¢Ð ÐžÐ™ÐšÐ˜ Ð¡Ð•Ð¢Ð˜      [0m
echo [93m=========================================[0m

:: ÐÐ²Ñ‚Ð¾Ð¼Ð°Ñ‚Ð¸Ñ‡ÐµÑÐºÐ¸Ð¹ Ð²Ñ‹Ð²Ð¾Ð´ Ð¼ÐµÐ½ÑŽ Ð¸Ð· Ð¼Ð°ÑÑÐ¸Ð²Ð°
for /f "tokens=2 delims=[]" %%i in ('set menu_items[') do (
    echo [96m %%i. !menu_items[%%i]![0m
    set "choice_keys=!choice_keys!%%i"
)
echo [93m=========================================[0m

echo.
choice /C "!choice_keys!" /n /m "[93m[?] Ð’Ñ‹Ð±ÐµÑ€Ð¸Ñ‚Ðµ Ð¿ÑƒÐ½ÐºÑ‚:[0m"
set /a "idx_fix=!errorlevel!-1"
set "choice=!choice_keys:~%idx_fix%,1!"

if "%choice%"=="1" goto mode_gaming
if "%choice%"=="2" goto mode_download
if "%choice%"=="3" goto mode_default
if /i "%choice%"=="x" endlocal&exit
goto menu


:: end of a function
:endfunc
echo.&echo [36m[!time!] Ð’Ñ‹Ð¿Ð¾Ð»Ð½ÐµÐ½Ð¸Ðµ Ð·Ð°Ð²ÐµÑ€ÑˆÐµÐ½Ð¾^^!
if !exaf!==1 (endlocal&exit/b)
echo ÐÐ°Ð¶Ð¼Ð¸Ñ‚Ðµ Ð»ÑŽÐ±ÑƒÑŽ ÐºÐ½Ð¾Ð¿ÐºÑƒ, Ñ‡Ñ‚Ð¾Ð±Ñ‹ Ð²ÐµÑ€Ð½ÑƒÑ‚ÑŒÑÑ Ð² Ð³Ð»Ð°Ð²Ð½Ð¾Ðµ Ð¼ÐµÐ½ÑŽ...[0m
pause>nul&endlocal&cls
goto :menu


:mode_gaming
cls
echo [^>] ÐŸÑ€Ð¸Ð¼ÐµÐ½ÑÑŽ Ð¸Ð³Ñ€Ð¾Ð²Ð¾Ð¹ Ð¿Ñ€Ð¾Ñ„Ð¸Ð»ÑŒ...
call :set_autotuning "normal"
call :set_fastopen "1"
call :set_dca "1"
call :set_ecn "0"
call :set_rss "1"
call :set_rsc "0"
call :set_nodelay "1"

:: Ð”Ð¾Ð¿. Ð½Ð°ÑÑ‚Ñ€Ð¾Ð¹ÐºÐ¸ Ñ‡ÐµÑ€ÐµÐ· Ñ€ÐµÐµÑÑ‚Ñ€ Ð¸ PS
powershell -NoLogo -NoProfile -ExecutionPolicy Bypass -Command ^
 "$a = Get-NetAdapter | Where-Object Status -eq 'Up';" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*InterruptModeration' -RegistryValue '0' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*PriorityVLANTag' -RegistryValue '0' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*ReceiveSideScaling' -RegistryValue '1' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*FlowControl' -RegistryValue '0' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*NumRssQueues' -RegistryValue '4' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*EEE' -RegistryValue '0' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Set-NetAdapterAdvancedProperty -RegistryKeyword '*IdleRestriction' -RegistryValue '0' -ErrorAction SilentlyContinue | Out-Null;" ^
 "$a | Disable-NetAdapterPowerManagement;" ^
 "exit 0;"

reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Multimedia\SystemProfile" /v "NetworkThrottlingIndex" /t REG_DWORD /d 0xffffffff /f >nul
reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Multimedia\SystemProfile" /v "SystemResponsiveness" /t REG_DWORD /d 0 /f >nul
reg add "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters" /v "MaxUserPort" /t REG_DWORD /d 65534 /f >nul
reg add "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters" /v "TcpMaxDataRetransmissions" /t REG_DWORD /d 2 /f >nul

call :restart_net_adapter

echo.
echo Ð“Ð¾Ñ‚Ð¾Ð²Ð¾^^!
goto endfunc



:mode_download
:: Throughput
cls
echo [^>] ÐŸÑ€Ð¸Ð¼ÐµÐ½ÑÑŽ Ð¿Ñ€Ð¾Ñ„Ð¸Ð»ÑŒ Ð´Ð»Ñ Ð·Ð°ÐºÐ°Ñ‡ÐµÐº...
call :set_autotuning "normal"
call :set_dca "1"
call :set_ecn "0"
call :set_rss "1"
call :set_rsc "1"
call :set_timestamps "1"
call :set_nodelay "0"

powershell -Command "Get-NetAdapter | Where-Object Status -eq 'Up' | Set-NetAdapterAdvancedProperty -RegistryKeyword '*InterruptModeration' -RegistryValue '1' -ErrorAction SilentlyContinue | Out-Null"
:: ÐÐ°ÑÑ‚Ñ€Ð¾Ð¹ÐºÐ° TCP Window
reg add "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters" /v "TcpWindowSize" /t REG_DWORD /d 65535 /f >nul
reg add "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Multimedia\SystemProfile" /v "NetworkThrottlingIndex" /t REG_DWORD /d 10 /f

call :restart_net_adapter

echo.
echo Ð“Ð¾Ñ‚Ð¾Ð²Ð¾!
goto endfunc



:mode_default
:: Restore defaults
cls
echo [^>] Ð’Ð¾Ð·Ð²Ñ€Ð°Ñ‚ Ðº Ð·Ð°Ð²Ð¾Ð´ÑÐºÐ¸Ð¼ Ð½Ð°ÑÑ‚Ñ€Ð¾Ð¹ÐºÐ°Ð¼ Windows ...

:: ÐžÑ‡Ð¸ÑÑ‚ÐºÐ° DNS
ipconfig /flushdns >nul
:: Ð¡Ð±Ñ€Ð¾Ñ IP/TCP
netsh int ip reset >nul
netsh int tcp reset >nul
:: Ð¡Ð±Ñ€Ð¾Ñ Winsock
netsh winsock reset >nul
:: CÐ±Ñ€Ð¾Ñ ÑˆÐ°Ð±Ð»Ð¾Ð½Ð¾Ð² TCP
netsh int tcp set supplemental template=internet setup
netsh int tcp set global autotuninglevel=normal
:: Ð£Ð´Ð°Ð»ÑÐµÐ¼ Ð¿Ð¾Ð»ÑŒÐ·Ð¾Ð²Ð°Ñ‚ÐµÐ»ÑŒÑÐºÐ¸Ðµ Ñ€ÐµÐµÑÑ‚Ñ€Ð¾Ð²Ñ‹Ðµ ÐºÐ»ÑŽÑ‡Ð¸
reg delete "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Multimedia\SystemProfile" /v NetworkThrottlingIndex /f >nul
reg delete "HKLM\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Multimedia\SystemProfile" /v SystemResponsiveness /f >nul
reg delete "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters" /v MaxUserPort /f >nul
reg delete "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters" /v TcpMaxDataRetransmissions /f >nul
for /F "tokens=1,2*" %%i in ('reg query HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters\Interfaces /s ^| findstr /I "Interface"') do (
    reg delete "%%j" /v TcpAckFrequency /f 2>nul
    reg delete "%%j" /v TCPNoDelay /f 2>nul
    reg delete "%%j" /v TcpDelAckTicks /f 2>nul
)

call :restart_net_adapter

echo Ð¡Ð±Ñ€Ð¾Ñ Ð·Ð°Ð²ÐµÑ€ÑˆÑ‘Ð½. ÐŸÐµÑ€ÐµÐ·Ð°Ð³Ñ€ÑƒÐ·Ð¸Ñ‚Ðµ ÐºÐ¾Ð¼Ð¿ÑŒÑŽÑ‚ÐµÑ€, Ñ‡Ñ‚Ð¾Ð±Ñ‹ Ð¸Ð·Ð¼ÐµÐ½ÐµÐ½Ð¸Ñ Ð²ÑÑ‚ÑƒÐ¿Ð¸Ð»Ð¸ Ð² ÑÐ¸Ð»Ñƒ.
goto endfunc


:restart_net_adapter
echo.&echo [90mÐ¿ÐµÑ€ÐµÐ·Ð°Ð¿ÑƒÑÐº ÑÐµÑ‚ÐµÐ²Ð¾Ð³Ð¾ Ð°Ð´Ð°Ð¿Ñ‚ÐµÑ€Ð°...[0m
powershell -NoLogo -NoProfile -ExecutionPolicy Bypass -Command ^
Get-NetAdapter ^| Where-Object { $_.Status -eq 'Up' } ^| Restart-NetAdapter -Confirm^:$false
exit/b


:set_autotuning
netsh int tcp set global autotuninglevel=%~1 >nul
exit /b


:set_ecn
if "%~1"=="0" (
    netsh int tcp set global ecncapability=disabled >nul 2>&1
) else (
    netsh int tcp set global ecncapability=enabled >nul 2>&1
)
exit /b


:set_dca
if "%~1"=="0" (netsh int tcp set global dca=disabled >nul) else (netsh int tcp set global dca=enabled >nul)
exit /b


:set_rss
if "%~1"=="0" (
    netsh int tcp set global rss=disabled >nul
    powershell -Command "Disable-NetAdapterRss -Name '*'" >nul 2>&1
) else (
    netsh int tcp set global rss=enabled >nul
    powershell -Command "Enable-NetAdapterRss -Name '*'" >nul 2>&1
)
exit /b


:set_rsc
if "%~1"=="0" (
    netsh int tcp set global rsc=disabled >nul
    powershell -Command "Disable-NetAdapterRsc -Name '*'" >nul 2>&1
) else (
    netsh int tcp set global rsc=enabled >nul
    powershell -Command "Enable-NetAdapterRsc -Name '*'" >nul 2>&1
)
exit /b


:set_timestamps
if "%~1"=="0" (netsh int tcp set global timestamps=disabled >nul) else (netsh int tcp set global timestamps=enabled >nul)
exit /b


:set_nodelay
if "%~1"=="1" (
    for /f %%i in ('reg query "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters\Interfaces"') do (
        reg add "%%i" /v "TcpAckFrequency" /t REG_DWORD /d 1 /f >nul 2>&1
        reg add "%%i" /v "TCPNoDelay" /t REG_DWORD /d 1 /f >nul 2>&1
        reg add "%%i" /v "TcpDelAckTicks" /t REG_DWORD /d 0 /f >nul 2>&1
    )
) else (
    for /f %%i in ('reg query "HKLM\SYSTEM\CurrentControlSet\Services\Tcpip\Parameters\Interfaces"') do (
        reg delete "%%i" /v "TcpAckFrequency" /f >nul 2>&1
        reg delete "%%i" /v "TCPNoDelay" /f >nul 2>&1
        reg delete "%%i" /v "TcpDelAckTicks" /f >nul 2>&1
    )
)
exit /b


:set_fastopen
if "%~1"=="0" (
    netsh int tcp set global fastopen=disabled >nul
) else (
    netsh int tcp set global fastopen=enabled >nul
)
exit /b
